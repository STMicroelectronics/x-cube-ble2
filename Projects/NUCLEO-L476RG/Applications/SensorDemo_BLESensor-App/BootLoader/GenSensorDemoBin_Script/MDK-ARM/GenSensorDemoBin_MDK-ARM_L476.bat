::/**
::  ******************************************************************************
::  * @file    GenSensorDemoBin_MDK-ARM_L476.bat
::  * @author  SRA Application Team
::  * @brief   Script for adding the bootloader part to a binary file
::  ******************************************************************************
::  * @attention
::  *
::  * Copyright (c) 2020 STMicroelectronics.
::  * All rights reserved.
::  *
::  * This software is licensed under terms that can be found in the LICENSE file
::  * in the root directory of this software component.
::  * If no LICENSE file comes with this software, it is provided AS-IS.
::  *
::  ******************************************************************************
::  */
@echo off
:: 1)Set the path to STLINK
::   Example: set STLINK_PATH="C:\Program Files (x86)\STMicroelectronics\STM32 ST-LINK Utility\ST-LINK Utility\"
set STLINK_PATH="C:\Program Files (x86)\STMicroelectronics\STM32 ST-LINK Utility\ST-LINK Utility\"

:: 2)Set the path to APPNAME
::   Example: set APPNAME=..\..\..\MDK-ARM\SensorDemo_BLESensor-App
:: NOTE: This script requires a .bin file is generated by the MDK-ARM build process.
::       To generate a .bin file, from MDK-ARM project, go to "Project -> Options for Target '<target name>' -> User", 
::       check "Run #1" and add the following User Command: 
::       "$K\ARM\ARMCC\bin\fromelf.exe --bin --output=@L.bin !L"
set APPNAME=..\..\..\MDK-ARM\SensorDemo_BLESensor-App

:: 3)Set the path to BOOTLOADER
::   Example: set BOOTLOADER="..\..\STM32L476RG_BL\BootLoaderL4.bin"
set BOOTLOADER="..\..\STM32L476RG_BL\BootLoaderL4.bin"

color 0F
echo                /******************************************/
echo                       Clean SensorDemo_BLESensor-App      
echo                /******************************************/
echo                              Full Chip Erase
echo                /******************************************/
%STLINK_PATH%ST-LINK_CLI.exe -c UR -HardRst -ME
echo                /******************************************/
echo                              Install BootLoader
echo                /******************************************/
%STLINK_PATH%ST-LINK_CLI.exe -P %BOOTLOADER% 0x08000000 -V "after_programming"
echo                /******************************************/
echo                      Install SensorDemo_BLESensor-App     
echo                /******************************************/
%STLINK_PATH%ST-LINK_CLI.exe -P %APPNAME%.bin 0x08004000 -V "after_programming"
echo                /******************************************/
echo                 Dump SensorDemo_BLESensor-App + BootLoader
echo                /******************************************/
set offset_size=0x4000
for %%I in (%APPNAME%.bin) do set application_size=%%~zI
echo %APPNAME%.bin size is %application_size% bytes
set /a size=%offset_size%+%application_size%
echo Dumping %offset_size% + %application_size% = %size% bytes ...
echo ..........................
%STLINK_PATH%ST-LINK_CLI.exe -Dump 0x08000000 %size% %APPNAME%_BL.bin
echo                /******************************************/
echo                                 Reset STM32
echo                /******************************************/
%STLINK_PATH%ST-LINK_CLI.exe -Rst
if NOT "%1" == "SILENT" pause